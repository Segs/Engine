include(CMakeDependentOption)
set(module_list)

macro(set_module_properties tgt name)
    set_common_target_properties(${tgt}_${name})
    set_target_properties (${tgt}_${name} PROPERTIES FOLDER ${tgt}/modules/ )
endmacro()
# Visit each subdirectory, see if it has CMakeLists.txt
# For each global target add an option to enable located module.
macro(detect_modules)
    set(includes_cpp "")
    set(register_cpp "")
    set(unregister_cpp "")
    file(GLOB files "*/CMakeLists.txt")
    file(GLOB incfiles "*/sources.cmake")
    list(APPEND files ${incfiles})
    list(SORT files) # so register_module_types does not change that often, and also plugins are registered in alphabetic order
    foreach(file ${files})
        get_filename_component(module_dir ${file} DIRECTORY)
        get_filename_component(module_name ${module_dir} NAME)
        list(APPEND module_list ${module_name})
        string(TOUPPER ${module_name} upname)
        foreach(tgt ${global_targets})
            OPTION(MODULE_${tgt}_${upname}_ENABLE "Enable module '${module_name}'" ON)
        endforeach()
        if(EXISTS "${module_dir}/register_types.h")
            string(APPEND includes_cpp "#include \"modules/${module_name}/register_types.h\"\n")
            string(TOUPPER ${module_name} module_upper)
            string(APPEND register_cpp "#ifdef MODULE_${module_upper}_ENABLED\n")
            string(APPEND register_cpp "    register_${module_name}_types();\n")
            string(APPEND register_cpp "#endif\n")
            string(APPEND unregister_cpp "#ifdef MODULE_${module_upper}_ENABLED\n")
            string(APPEND unregister_cpp "    unregister_${module_name}_types();\n")
            string(APPEND unregister_cpp "#endif\n")
        endif()
    endforeach()
    configure_file(register_module_types.cpp.cmake register_module_types.gen.cpp)
endmacro()

macro(create_simple_module_target name)
    file(GLOB_RECURSE source_files "*.cpp" )
    file(GLOB_RECURSE header_files "*.h" )
    file(GLOB_RECURSE qrc_files "*.qrc" )

    foreach(tgt ${global_targets})
        string(TOUPPER ${name} upname)
        if(MODULE_${tgt}_${upname}_ENABLE)
            list(APPEND module_resources ${qrc_files})
            list(APPEND module_sources ${source_files} ${header_files})
            list(APPEND module_3rdparty ${thirdparty_sources})
            set(module_resources ${module_resources} PARENT_SCOPE)
            set(module_sources ${module_sources} PARENT_SCOPE)
            set(module_3rdparty ${module_3rdparty} PARENT_SCOPE)
        endif()
    endforeach()
endmacro()
macro(create_module_target name)
    file(GLOB_RECURSE source_files "*.cpp" )
    file(GLOB_RECURSE header_files "*.h" )
    file(GLOB_RECURSE qrc_files "*.qrc" )

    foreach(tgt ${global_targets})
        string(TOUPPER ${name} upname)
        if(MODULE_${tgt}_${upname}_ENABLE)
            list(APPEND module_resources ${qrc_files})
            list(APPEND module_sources ${source_files} ${header_files})
            list(APPEND module_3rdparty ${thirdparty_sources})
            list(APPEND module_deps ${ARGN})

            set(module_resources ${module_resources} PARENT_SCOPE)
            set(module_sources ${module_sources} PARENT_SCOPE)
            set(module_3rdparty ${module_3rdparty} PARENT_SCOPE)
            set(module_deps ${module_deps} PARENT_SCOPE)
            target_compile_definitions(${tgt}_modules PUBLIC MODULE_${upname}_ENABLED)
        endif()
    endforeach()
endmacro()
macro(add_module tgt name)
    string(TOUPPER ${name} upname)
    if(MODULE_${tgt}_${upname}_ENABLE)
        add_subdirectory(${name})
        target_compile_definitions(${tgt}_modules PUBLIC MODULE_${upname}_ENABLED)
        target_link_libraries(${tgt}_modules PRIVATE ${tgt}_module_${name})
    endif()
endmacro()

detect_modules()
CMAKE_DEPENDENT_OPTION(MODULE_VORBIS_ENABLE "Enable module 'vorbis'" ON
                       "NOT MODULE_STB_VORBIS_ENABLE" OFF)

set(module_sources_gen ${CMAKE_CURRENT_BINARY_DIR}/register_module_types.gen.cpp)
set(module_3rdparty)
set(module_deps)
set(module_resources)

foreach(tgt ${global_targets})
    add_library(${tgt}_modules STATIC)
    set_property(TARGET ${tgt}_modules PROPERTY POSITION_INDEPENDENT_CODE ON)

    set_target_properties(${tgt}_modules PROPERTIES AUTORCC ON)


    foreach(module_dir ${module_list})
        if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/${module_dir}/sources.cmake")
            string(TOUPPER ${module_dir} upname)
            if(MODULE_${tgt}_${upname}_ENABLE)
                include(${module_dir}/sources.cmake)
                target_compile_definitions(${tgt}_modules PUBLIC MODULE_${upname}_ENABLED)
            endif()
        elseif(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/${module_dir}/register_types.h")
            message("WILL LINK OBJECT ${module_dir}")
            add_module(${tgt} ${module_dir})
        else()
            #
        endif()
    endforeach()
    target_sources(${tgt}_modules PRIVATE ${module_sources} ${module_sources_gen} ${module_3rdparty})

    add_library(${tgt}_module_resources OBJECT ${module_resources})
    set_property(TARGET ${tgt}_module_resources PROPERTY POSITION_INDEPENDENT_CODE ON)
    set_target_properties(${tgt}_module_resources PROPERTIES AUTORCC ON)

    foreach(src ${module_sources_gen})
        set_source_files_properties(${src} PROPERTIES GENERATED TRUE)
    endforeach()
    target_link_libraries(${tgt}_modules PRIVATE ${tgt}_core ${module_deps} ${tgt}_module_resources)
    set_common_target_properties(${tgt}_modules)
    if(USE_UNITY_BUILDS)
        set_target_properties(${tgt}_modules PROPERTIES UNITY_BUILD ON)
        set_target_properties(${tgt}_modules PROPERTIES UNITY_BUILD_BATCH_SIZE ${DEFAULT_UNITY_BATCH_SIZE})
        if(MSVC)
            target_compile_options(${tgt}_modules PRIVATE /bigobj)
        endif()
    endif()

endforeach()
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${module_sources})
source_group(TREE ${PROJECT_SOURCE_DIR} FILES ${module_3rdparty})
source_group(Generated FILES ${module_sources_gen})
INSTALL (
    DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    DESTINATION include/SegsEngine
    FILES_MATCHING PATTERN "*.h"
)
